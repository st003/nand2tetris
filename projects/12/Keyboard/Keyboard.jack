// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int KEYBOARD;

    /** Initializes the keyboard. */
    function void init() {

        // location in ram of the keyboard memory map
        // as specified for the Hack computer
        let KEYBOARD = 24576;

        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var int key;
        let key = Memory.peek(KEYBOARD);
        if (key > 0) {
            return key;
        } else {
            return 0;
        }
    }

    /**	Waits until a key is pressed on the keyboard and released,
     *  then echoes the key to the screen, and returns the character
     *  of the pressed key. */
    function char readChar() {
        var char key;
        let key = 0;

        // wait for the keyboard to be pressed
        while (Keyboard.keyPressed() = 0) {}

        // get the key which is pressed
        let key = Keyboard.keyPressed();

        // wait until no keys are pressed
        while (~(Keyboard.keyPressed() = 0)) {}

        // backspace
        if (key = 129) {
            do Output.backSpace();
        } else {
            // don't print a the newline character
            if (~(key = 128)) {
                do Output.printChar(key);
            }
        }

        return key;
    }

    /**	Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its value. Also handles user backspaces. */
    function String readLine(String message) {

        var String str;
        var char c;

        let str = String.new(64); // 64 is arbitrary but decided upon b/c 64 max columns

        do Output.printString(message);

        while (true) {
            let c = Keyboard.readChar();

            // newline
            if (c = 128) {
                do Output.println();
                return str;

            } else {

                // backspace
                if (c = 129) {
                    do str.eraseLastChar();

                } else {
                    do str.appendChar(c);
                }
            }
        }

        return str;
    }

    /** Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its integer value (until the first non-digit character in the
     *  entered text is detected). Also handles user backspaces. */
    function int readInt(String message) {
        // TODO: implement
        return 0;
    }
}
