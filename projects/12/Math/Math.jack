// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int i, tmp;

        // width of Hack CPU instructions
        let n = 16;

        // cache the numbers representing bit masks for each bit
        let powersOfTwo = Array.new(n);
        let i = 0;
        let tmp = 1;
        while(i < n) {
            let powersOfTwo[i] = tmp;
            let tmp = tmp + tmp;
            let i = i + 1;
        }
        return;
    }

    /** checks if the bit for n in the i'th position is high
     *  i is z-indexed */
    function boolean bit(int n, int i) {
        return (~((n & powersOfTwo[i]) = 0));
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method.
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {

        var int sum, shiftedX;
        var int i, w;

        let sum = 0;
        let shiftedX = x;

        // iterate each bit
        let i = 0;
        while(i < n) {

            // check if the i'th bit is high
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }

            // right-pad x by one bit
            let shiftedX = shiftedX + shiftedX;

            // shift the high bit up one place
            let i = i + 1;
        }

        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {

        var int q;

        // using negative numbers internally causes
        // an infinite loop
        var boolean isNeg;
        let isNeg = false;

        if (x < 0) {
            let x = Math.abs(x);
            let isNeg = true;
        }

        if (y < 0) {
            let y = Math.abs(y);
            let isNeg = true;
        }

        // two negatives should result in a positive
        if ((x < 0) & (y < 0)) {
            let isNeg = false;
        }

        // early exit and overflow prevention
        if ((y > x) | (y < 0)) {
            return 0;
        }

        // recursively call divide with a larger and larger y
        // this works b/c divide will eventually early exit
        let q = Math.divide(x, (y * 2));

        if ((x - (2 * q * y)) < y) {
            let q = 2 * q;
        } else {
            let q = (2 * q) + 1;
        }

        if (isNeg) {
            return q * -1;
        }

        return q;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {

        var int y, j, tmp, check;

        let y = 0;
        let j = (n / 2) - 1;

        while(j > -1) {

            let tmp = y + powersOfTwo[j];
            let check = tmp * tmp;

            if (~(check > x) & (check > 0)) {
                let y = tmp;
            }

            let j = j - 1;
        }

        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return x * -1;
        }
        return x;
    }
}
