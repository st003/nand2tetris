// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    static int SCREEN;
    static boolean currentColor;

    /** Initializes the Screen. */
    function void init() {

        // Hack computer screen starts here in memory
        let SCREEN = 16384;

        // default the current color to black
        let currentColor = true;

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int addr;
        let addr = SCREEN;
        while (addr < 24576) {
            do Memory.poke(addr, 0);
            let addr = addr + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let currentColor = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {

        var int addr, value, bitMask;
        var int r;

        // get the word which needs to be modified
        let addr = SCREEN + (32 * y) + (x / 16);

        // get the current value of the word so we can
        // modify only the specified but without changing
        // the whole word
        let value = Memory.peek(addr);

        // optimization: when dealing with powers of 2, (n & 15) is the same as Math.modulus(n, 16)
        let r = x & 15;

        // create a bit mask from the remainder
        let bitMask = Math.getPowerOfTwo(r);

        // black
        if (currentColor) {
            // bitwise OR sets the specific bit high
            let value = value | bitMask;

        // white
        } else {
            // negating the mask and bitwise AND sets the specific bit low
            let value = value & ~bitMask;
        }

        // set the modified word in memory
        do Memory.poke(addr, value);

        return;
    }

    /** Optimized line draw for horizonal lines only */
    function void drawHorizontalLine(int x1, int x2, int y) {

        var int tmp, leftWord, rightWord;
        var int mask, value, leftBit, rightBit;

        // always draw right to left
        if (x1 > x2) {
            let tmp = x1;
            let x1 = x2;
            let x2 = tmp;
        }

        let leftWord = SCREEN + (32 * y) + (x1 / 16);
        let rightWord = SCREEN + (32 * y) + (x2 / 16);

        // if the start and end occupy the same word
        if (leftWord = rightWord) {
            // less than or equal to
            while(~(x1 > x2)) {
                do Screen.drawPixel(x1, y);
                let x1 = x1 + 1;
            }
            return;
        }

        // for multiple words, start with the left word
        let mask = 0;

        // optimization: when dealing with powers of 2, (n & 15) is the same as Math.modulus(n, 16)
        let leftBit = x1 & 15;
        if (leftBit = 0) {
            // full word
            do Memory.poke(leftWord, currentColor);

        } else {
            while (~(leftBit > 15)) {
                let mask = mask + Math.getPowerOfTwo(leftBit);
                let leftBit = leftBit + 1;
            }
            // draw white
            if (~currentColor) {
                let value = Memory.peek(leftWord);
                let mask = value & ~mask;
            }
            do Memory.poke(leftWord, mask);
        }

        // middle section
        // start from the first word after the left word
        let leftWord = leftWord + 1;
        while (leftWord < rightWord) {
            do Memory.poke(leftWord, currentColor);
            let leftWord = leftWord + 1;
        }

        // right word
        let mask = 0;

        // optimization: when dealing with powers of 2, (n & 15) is the same as Math.modulus(n, 16)
        let rightBit = x2 & 15;
        if (rightBit = 15) {
            // full word
            do Memory.poke(leftWord, currentColor);

        } else {
            while (~(rightBit < 0)) {
                let mask = mask + Math.getPowerOfTwo(rightBit);
                let rightBit = rightBit - 1;
            }
            // draw white
            if (~currentColor) {
                let value = Memory.peek(rightWord);
                let mask = value & ~mask;
            }
            do Memory.poke(rightWord, mask);
        }

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {

        var int dx, dy, tmp;
        var int xo, yo, diff;
        var int dyPos;

        // always draw left to right
        if (x1 > x2) {
            let tmp = x1;
            let x1 = x2;
            let x2 = tmp;

            // and in this case flip y1 and y2
            let tmp = y1;
            let y1 = y2;
            let y2 = tmp;
        }

        let dx = x2 - x1;
        let dy = y2 - y1;

        let xo = 0;
        let yo = 0;
        let diff = 0;

        // horizonal line
        if (dy = 0) {
            do Screen.drawHorizontalLine(x1, x2, y1);
            return;
        }

        // vertical line
        if (dx = 0) {

            // draw top to bottom
            if (dy > 0) {
                // less than or equal to
                while (~(yo > dy)) {
                    do Screen.drawPixel(x1, y1 + yo);
                    let yo = yo + 1;
                }

            // draw bottom to top
            } else {
                // greater than or equal to
                while (~(yo < dy)) {
                    do Screen.drawPixel(x1, y1 + yo);
                    let yo = yo - 1;
                }
            }

            return;
        }

        // diagonal lines
        let dyPos = Math.abs(dy); // force positive dy
        // while "less-than or equal to"
        while (~(xo > dx) & ~(yo > dyPos)) {
            do Screen.drawPixel(x1 + xo, y1 + yo);
            if (diff < 0) {
                let xo = xo + 1;
                let diff = diff + dyPos;
            } else {
                if (dy > 0) {
                    let yo = yo + 1;
                } else {
                    // this works because even if the yo > dyPos never fails, the other condition will eventually
                    let yo = yo - 1;
                }
                let diff = diff - dx;
            }
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     *  and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        // TODO: implement
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        // TODO: implement
        return;
    }
}
