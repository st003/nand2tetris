// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

    field Array s;
    field int cap, length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {

        // Array throws an error with size 0
        if (maxLength = 0) {
            let maxLength = 1;
        }

        let s = Array.new(maxLength);
        let cap = maxLength;
        let length = 0;

        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do s.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if (~(j < 0) & (j < length)) {
            return s[j];
        } else {
            return 0;
        }
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if (~(j < 0) & (j < cap)) {
            let s[j] = c;
        }
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < cap) {
            let s[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string,
     *  until a non-digit character is detected. */
    method int intValue() {

        var int n, i;
        var boolean isNeg;
        var char c;

        let n = 0;
        let isNeg = false;

        let i = 0;
        while (i < length) {

            let n = n * 10; // increase 10's place
            let c = s[i];

            // check if first char is "-"
            if ((i = 0) & (c = 45)) {
                let isNeg = true;

            } else {
                if ((c > 47) & (c < 58)) {
                    // remove the offset to get the actual numerical value
                    let n = n + (c - 48);
                } else {
                    // exit the loop on a non-number char
                    let i = length;
                }
            }

            let i = i + 1;
        }

        if (isNeg) {
            let n = -n;
        }

        return n;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {

        // move the internal string cursor to the beginning
        let length = 0;

        if (val < 0) {
            let val = -val;
            do appendChar(45); // decimal value for '-'
        }

        do setIntChars(val);
        return;
    }

    /** Converts a positive int to a sequence of characters and inserts
    * them into the current string. */
    method void setIntChars(int val) {

        var int q, r, c;

        // reduce the size of val for next iteration
        let q = val / 10;

        // get the right-most digit from val
        let r = val - (q * 10);

        // add ASCII offset to convert into char representation
        let c = r + 48;

        // by making this recursive, the digits from the left side are
        // inserted into the array first. When the value is less than
        // 10, that's you recursive exit
        if (val < 10) {
            do appendChar(c);

        } else {
            do setIntChars(q);
            do appendChar(c);
        }

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        // Decimal value for newline per Unit 6.9
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        // Decimal value for backspace per Unit 6.9
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        // ASCII decimal value for double-quote
        return 34;
    }
}
