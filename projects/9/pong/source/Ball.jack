class Ball {

    field int width;
    field int height;
    field int x;
    field int y;
    field int baseXSpeed;
    field int baseYSpeed;
    field int xSpeed;
    field int ySpeed;

    constructor Ball new() {

        let width = 6;
        let height = 6;
        let x = 253;
        let y = 128 + height;
        let baseXSpeed = 3;
        let baseYSpeed = 0; // TODO: set this initial value "randomly"?
        let xSpeed = baseXSpeed;
        let ySpeed = baseYSpeed;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void update(ScoreBoard scoreBoard) {

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update
        do clear();

        let x = x + xSpeed;
        let y = y + ySpeed;

        // score on player
        if (x < 0) {
            do scoreBoard.incrementCpuScore();
            do reset(1);
        }

        // score on cpu
        if (x + width > 511) {
            do scoreBoard.incrementPlayerScore();
            do reset(-1);
        }

        // top of screen
        if (y < 32) {
            let y = 32;
            let ySpeed = ySpeed * -1;
        }

        // bottom of screen
        if (y + height > 255) {
            let y = 255 - height;
            let ySpeed = ySpeed * -1;
        }

        return;
    }

    /**
    *  Resets the ball to the center and flips its direction
    *  left = -1
    *  right = 1
    */
    method void reset(int direction) {
        let x = 253;
        let y = 128 + height;
        let xSpeed = baseXSpeed * direction;
        // TODO: set ySpeed
        return;
    }

    method void clear() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }
}
