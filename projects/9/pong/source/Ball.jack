class Ball {

    field int width;
    field int height;
    field int x;
    field int y;
    field int baseSpeed;
    field int speed;

    constructor Ball new() {

        let width = 6;
        let height = 6;
        let x = 253;
        let y = 128 + height;
        let baseSpeed = 3;
        let speed = baseSpeed;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void update(ScoreBoard scoreBoard) {

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update
        do clear();

        let x = x + speed;

        // score on player
        if (x < 0) {
            do scoreBoard.incrementCpuScore();
            do reset(1);
        }

        // score on cpu
        if (x + width > 511) {
            do scoreBoard.incrementPlayerScore();
            do reset(-1);
        }

        return;
    }

    /**
    *  Resets the ball to the center and flips its direction
    *  left = -1
    *  right = 1
    */
    method void reset(int direction) {
        let x = 253;
        let y = 128 + height;
        let speed = baseSpeed * direction;
        return;
    }

    method void clear() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }
}
