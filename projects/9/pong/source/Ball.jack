class Ball {

    field int width;
    field int height;
    field int x;
    field int y;
    field int baseXSpeed;
    field int xSpeed;
    field int ySpeed;

    constructor Ball new() {

        let width = 6;
        let height = 6;
        let x = 253;
        let y = 134;
        let baseXSpeed = 2;
        let xSpeed = baseXSpeed;
        let ySpeed = 0;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void randYSpeed(char key) {
        let ySpeed = Ball.randomY(key);
        return;
    }

    method void increaseXSpeed() {
        if (xSpeed > 0) {
            let xSpeed = xSpeed + 1;
        }

        if (xSpeed < 0) {
            let xSpeed = xSpeed - 1;
        }

        return;
    }

    method boolean collides(int altX, int altWidth, int altY, int altHeight) {

        // first, check to see if the left edge of either is farther to the right
        // than the right edge of the other

        if (x > (altX + altWidth)) {
            return false;
        }

        if (altX > (x + width)) {
            return false;
        }

        // then check to see if the bottom edge of either is higher than the top
        // edge of the other

        if (y > (altY + altHeight)) {
            return false;
        }

        if (altY > (y + height)) {
            return false;
        }

        // if the above aren't true, they're overlapping
        return true;
    }

    method void update(ScoreBoard sb, PlayerPaddle p, CpuPaddle c) {

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update
        do clear();

        let x = x + xSpeed;
        let y = y + ySpeed;

        // player paddle
        if (collides(p.getX(), p.getWidth(), p.getY(), p.getHeight())) {
            let x = p.getX() + p.getWidth();
            let xSpeed = xSpeed * -1;
            do increaseXSpeed();
        }

        // cpu paddle
        if (collides(c.getX(), c.getWidth(), c.getY(), c.getHeight())) {
            let x = c.getX() - width;
            let xSpeed = xSpeed * -1;
            do increaseXSpeed();
        }

        // top of screen
        if (y < 32) {
            let y = 32;
            let ySpeed = ySpeed * -1;
        }

        // bottom of screen
        if (y + height > 255) {
            let y = 255 - height;
            let ySpeed = ySpeed * -1;
        }

        // score on player
        if (x < 0) {
            do sb.incrementCpuScore();
            do reset(1);
            return;
        }

        // score on cpu
        if (x + width > 511) {
            do sb.incrementPlayerScore();
            do reset(-1);
            return;
        }

        return;
    }

    /**
    *  Resets the ball to the center and flips its direction
    *  left = -1
    *  right = 1
    */
    method void reset(int xDirection) {
        var int temp;
        let temp = y;

        let x = 253;
        let y = 128 + height;

        let xSpeed = baseXSpeed * xDirection;
        let ySpeed = Ball.randomY(temp);

        return;
    }

    method void clear() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    // takes a seed and returns a number between -2 and 2 (not 0)
    function int randomY(int seed) {

        var int y;
        var int range;
        var int flip;

        let y = seed;
        let flip = 1;
        let range = 2;

        while (y - range > 0) {
            let y = y - range;
            let flip = flip * -1;
        }

        return y * flip;
    }
}
