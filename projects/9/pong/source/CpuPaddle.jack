/** The cpu's paddle entity*/
class CpuPaddle {

    field int width;
    field int height;
    field int x;
    field int y;
    field int speed;

    constructor CpuPaddle new() {

        let width = 4;
        let height = 36;
        let x = 495 - width;
        let y = 124;
        let speed = 3;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method int getWidth() {
        return width;
    }

    method int getHeight() {
        return height;
    }

    method int getX() {
        return x;
    }

    method int getY() {
        return y;
    }

    method void update(Ball b) {

        var boolean ballIsMovingRight;
        var boolean ballIsCloseEnough;
        var int paddleCenter;

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update
        do clear();

        let ballIsMovingRight = b.getXSpeed() > 0;
        let ballIsCloseEnough = (b.getX() + b.getWidth()) > 255; // higher number makes the cpu worse

        // "AI" for cpu. Cpu "reaction time" is based on what direction and where the
        // ball currently is
        if (ballIsMovingRight & ballIsCloseEnough) {

            let paddleCenter = y + (height / 2);

            // move up
            if (b.getY() < paddleCenter) {
                // collision detection for score board
                if ((y - speed) > 32) {
                    let y = y - speed;
                } else {
                    let y = 32;
                }
            }

            // move down
            if (b.getY() > paddleCenter) {
                // collision detection for bottom of screen
                if ((y + height + speed) < 255) {
                    let y = y + speed;
                } else {
                    let y = 255 - height;
                }
            }
        }

        return;
    }

    method void clear() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }
}
