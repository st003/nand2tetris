/** The player's paddle entity*/
class PlayerPaddle {

    static char UP_KEY;
    static char DOWN_KEY;

    field int width;
    field int height;
    field int x;
    field int y;
    field int speed;

    constructor PlayerPaddle new() {

        // key bindings
        let UP_KEY = 119; // 'w'
        let DOWN_KEY = 115; // 's'

        let width = 4;
        let height = 36;
        let x = 16;
        let y = 124;
        let speed = 3;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method int getWidth() {
        return width;
    }

    method int getHeight() {
        return height;
    }

    method int getX() {
        return x;
    }

    method int getY() {
        return y;
    }

    /** Takes player input and moves the paddle up or down */
    method void update(char key) {

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update
        do clear();

        if (key = UP_KEY) {
            // collision detection for score board
            if ((y - speed) > 32) {
                let y = y - speed;
            } else {
                let y = 32;
            }
        }

        if (key = DOWN_KEY) {
            // collision detection for bottom of screen
            if ((y + height + speed) < 255) {
                let y = y + speed;
            } else {
                let y = 255 - height;
            }
        }

        return;
    }

    method void clear() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }
}
