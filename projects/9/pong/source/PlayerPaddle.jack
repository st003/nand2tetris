class PlayerPaddle {

    static char UP;
    static char DOWN;

    field int width;
    field int height;
    field int x;
    field int y;
    field int speed;

    constructor PlayerPaddle new() {

        // key bindings
        let UP = 119; // 'w'
        let DOWN = 115; // 's'

        let width = 4;
        let height = 32;
        let x = 16;
        let y = 128;
        let speed = 3;

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void update(char key) {

        // ideally this would be done in the draw method but
        // because we are not redrawing the whole frame each
        // iteration we need to clear the paddle from the
        // previous frame before calculating the update

        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + width), (y + height));

        if (key = UP) {
            // collision detection for score board
            if (y - speed > 32) {
                let y = y - speed;
            } else {
                let y = 32;
            }
        }

        if (key = DOWN) {
            // collision detection for bottom of screen
            if (y + height < 255) {
                let y = y + speed;
            } else {
                let y = 255 - height;
            }
        }

        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, (x + width), (y + height));
        return;
    }
}
