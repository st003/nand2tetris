class Pong {

    // classes with a constructor must have at least 1 field

    static char QUIT_KEY;

    static int START_STATE;
    static int PLAY_STATE;

    field boolean is_running;
    field int game_state;
    field ScoreBoard scoreBoard;
    field PlayerPaddle player;
    field CpuPaddle cpu;
    field Ball ball;

    constructor Pong new() {

        // key bindings
        let QUIT_KEY = 140; // 'esc'

        let START_STATE = 1;
        let PLAY_STATE = 2;

        do Screen.clearScreen();
        let is_running = false;
        let game_state = START_STATE;

        // entities
        let scoreBoard = ScoreBoard.new();
        let player = PlayerPaddle.new();
        let cpu = CpuPaddle.new();
        let ball = Ball.new();

        // display Instructions
        do Output.moveCursor(9, 24);
        do Output.printString("ESC - Quit Game");
        do Output.moveCursor(10, 24);
        do Output.printString("W - Move up");
        do Output.moveCursor(11, 24);
        do Output.printString("S - Move down");
        do Output.moveCursor(13, 18);
        do Output.printString("Press any Key to Start Game");

        return this;
    }

    method void dispose() {

        // clean up entities
        do scoreBoard.dispose();
        do player.dispose();
        do cpu.dispose();
        do ball.dispose();

        do Memory.deAlloc(this);
        return;
    }

    method void clearPlayArea() {
        do Screen.setColor(false);
        do Screen.drawRectangle(0, 32, 511, 255);
        return;
    }

    method void win(boolean playerWon) {
        do clearPlayArea();
        if (playerWon) {
            do Output.moveCursor(12, 27);
            do Output.printString("Player Wins!");
        } else {
            do Output.moveCursor(12, 28);
            do Output.printString("CPU Wins!");
        }
        return;
    }

    method void quit() {

        do clearPlayArea();

        do Output.moveCursor(12, 30);
        do Output.printString("Quit");

        return;
    }

    method void run() {

        // variable declarations must occur at the top scope of
        // a function or method
        var char key;
        var boolean playerWon;
        var boolean cpuWon;

        let is_running = true;

        // game loop
        while(is_running) {

            // lack of system clock method means be cannot calculate
            // real frame times or dealta time for animation. must
            // simulate framerate
            do Sys.wait(16); // ~60fps

            // read player input
            let key = Keyboard.keyPressed();

            if (key = QUIT_KEY) {
                do quit();
                return;
            }

            if (game_state = START_STATE) {
                if (key > 0) {
                    do clearPlayArea();
                    let game_state = PLAY_STATE;
                    do ball.randYSpeed(key);
                }
            }

            if (game_state = PLAY_STATE) {

                // update game state
                do player.update(key);
                do ball.update(scoreBoard);

                let playerWon = scoreBoard.playerWon();
                let cpuWon = scoreBoard.cpuWon();
                if (playerWon | cpuWon) {
                    do win(playerWon);
                    return;
                }

                // draw
                do scoreBoard.draw();
                do player.draw();
                do cpu.draw();
                do ball.draw();
            }
        }

        return;
    }
}
