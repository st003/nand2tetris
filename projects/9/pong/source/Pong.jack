class Pong {

    // classes with a constructor must have at least 1 field

    static char QUIT;

    field boolean is_running;
    field ScoreBoard scoreBoard;
    field PlayerPaddle player;

    constructor Pong new() {

        let is_running = false;

        // key bindings
        let QUIT = 113; // 'q'

        // entities
        let scoreBoard = ScoreBoard.new();
        let player = PlayerPaddle.new();

        return this;
    }

    method void dispose() {

        // clean up entities
        do scoreBoard.dispose();
        do player.dispose();

        do Memory.deAlloc(this);
        return;
    }

    method void run() {

        var char key;

        let is_running = true;

        // game loop
        while(is_running) {

            // lack of system clock method means be cannot calculate
            // real frame times or dealta time for animation. must
            // simulate framerate
            do Sys.wait(16); // ~60fps

            // read player input
            let key = Keyboard.keyPressed();

            if (key = QUIT) {
                let is_running = false;
            }

            // update game state
            do player.update(key);

            // draw
            do scoreBoard.draw();
            do player.draw();
        }

        do Screen.clearScreen();
        do Output.moveCursor(12, 30);
        do Output.printString("Quit");

        return;
    }
}
