class Pong {

    // classes with a constructor must have at least 1 field

    static char QUIT_KEY;
    static char START_KEY;

    static int START_STATE;
    static int PLAY_STATE;

    field boolean is_running;
    field int game_state;
    field ScoreBoard scoreBoard;
    field PlayerPaddle player;

    constructor Pong new() {

        // key bindings
        let QUIT_KEY = 113; // 'q'
        let START_KEY = 32; // 'space'

        let START_STATE = 1;
        let PLAY_STATE = 2;

        do Screen.clearScreen();
        let is_running = false;
        let game_state = START_STATE;

        // entities
        let scoreBoard = ScoreBoard.new();
        let player = PlayerPaddle.new();

        do Instructions.display();

        return this;
    }

    method void dispose() {

        // clean up entities
        do scoreBoard.dispose();
        do player.dispose();

        do Memory.deAlloc(this);
        return;
    }

    method void quit() {

        do player.clear();

        do Output.moveCursor(12, 30);
        do Output.printString("Quit");

        return;
    }

    method void run() {

        var char key;

        let is_running = true;

        // game loop
        while(is_running) {

            // lack of system clock method means be cannot calculate
            // real frame times or dealta time for animation. must
            // simulate framerate
            do Sys.wait(16); // ~60fps

            // read player input
            let key = Keyboard.keyPressed();

            if (key = QUIT_KEY) {
                let is_running = false;
            }

            if (game_state = START_STATE) {
                if (key = START_KEY) {
                    do Instructions.clear();
                    let game_state = PLAY_STATE;
                }
            }

            if (game_state = PLAY_STATE) {

                // update game state
                do player.update(key);

                // draw
                do scoreBoard.draw();
                do player.draw();
            }
        }

        do quit();
        return;
    }
}
