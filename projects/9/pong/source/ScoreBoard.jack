class ScoreBoard {

    static int maxScore;

    field int playerScore;
    field int cpuScore;

    constructor ScoreBoard new() {

        let maxScore = 1; // TODO: set final score

        let playerScore = 0;
        let cpuScore = 0;

        do Screen.setColor(true);

        // player score
        do Output.moveCursor(1, 1);
        do Output.printInt(playerScore);

        // draw title and header once (redraws of printString() cause heap overflows)
        // draw title
        do Output.moveCursor(1, 30);
        do Output.printString("PONG");

        // cpu score
        do Output.moveCursor(1, 62);
        do Output.printInt(cpuScore);

        // header border
        do Screen.drawLine(0, 31, 511, 31);

        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method boolean playerWon() {
        if (playerScore < maxScore) {
            return false;
        }
        return true;
    }

    method boolean cpuWon() {
        if (cpuScore < maxScore) {
            return false;
        }
        return true;
    }

    method void incrementPlayerScore() {
        let playerScore = playerScore + 1;
        return;
    }

    method void incrementCpuScore() {
        let cpuScore = cpuScore + 1;
        return;
    }

    method void draw() {

        do Screen.setColor(true);

        // player score
        do Output.moveCursor(1, 1);
        do Output.printInt(playerScore);

        // cpu score
        do Output.moveCursor(1, 62);
        do Output.printInt(cpuScore);

        return;
    }
}